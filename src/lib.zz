using base64

using "uuid.h"::{ uuid_generate }

export const usize MAX_INT = 268435456;
export const usize UUID_BYTES = 16;
// last byte is never used, but needed for initial b64 encoding before normalization
export const usize CONTEXT_ID_BYTES = 24;
// 10 for padding
export const usize RESULT_ID_BYTES = CONTEXT_ID_BYTES + 10;

export struct UUID {
  u8 bytes[UUID_BYTES];
}

export struct Options packed {
  bool fixed_length;
  bool url_safe;
}

export struct Context {
  UUID uuid;
  Options options;
  u32 count;
  u8 id[CONTEXT_ID_BYTES];

  Result result;
}

export struct Result {
  u8 bytes[RESULT_ID_BYTES];
}

export fn make(Context new mut *self, u32 offset, Options options)
{
  uuid_generate(self->uuid.bytes);

  self->options.fixed_length = options.fixed_length;
  self->options.url_safe = options.url_safe;

  self->count = offset;

  baseid(self->id, &self->uuid, self->options.url_safe);

  for (usize mut i = 0; i < 10; ++i) {
    static_attest(len(self->result.bytes) > CONTEXT_ID_BYTES - 1 + i);
    self->result.bytes[CONTEXT_ID_BYTES - 1 + i] = (u8) 0x30;
  }

  for (usize mut i = 0 ; i < CONTEXT_ID_BYTES - 1; ++i) {
    static_attest(len(self->result.bytes) > i);
    self->result.bytes[i] = self->id[i];
  }
}

export fn count(Context *self) -> u32 {
  return self->count;
}

export fn generate(Context mut *self, Result mut *out) -> usize
  where safe(out)
  where len(out->bytes) == RESULT_ID_BYTES
  model return <= RESULT_ID_BYTES
{
  usize mut counter_offset = 0;
  usize mut offset = 0;
  usize mut padded = log10(self->count) + 1;
  usize mut size = 0;

  let opts = &self->options;
  u8 mut *bytes = self->result.bytes;

  offset += CONTEXT_ID_BYTES - 1;

  for (usize mut i = 0; i < offset; ++i) {
    out->bytes[i] = bytes[i];
  }

  size += offset;

  if opts->fixed_length {
    counter_offset = 10;
    padded = 10;
  } else {
    counter_offset = padded;
  }

  size += padded;
  offset--;

  u32 mut n = self->count;
  for (usize mut i = counter_offset; i > 0; --i) {
    static_attest(len(out->bytes) > offset + i);
    out->bytes[offset + i] = (u8) (0x30 + (n % 10));
    n = div10(n);
  }

  self->count = self->count + 1;

  static_attest(size <= RESULT_ID_BYTES);
  return size;
}

export fn baseid(u8 mut *out, UUID *uuid, bool url_safe) -> usize
  where 24 == len(out)
  where 16 == len(uuid->bytes)
{
  usize mut size = base64::encode(out, uuid->bytes, 16);

  if url_safe {
    for (usize mut i = 0; i < size; ++i) {
      switch out[i] {
        (u8) '+' => { out[i] = (u8) '_'; }
        (u8) '/' => { out[i] = (u8) '-'; }
      }
    }

    static_attest(24 == size);
    if (u8) '=' == out[size - 1] {
      if (u8) '=' == out[size - 2] {
        out[size - 2] = (u8) '-';
        out[size - 1] = 0;
        size--;
      }
    }

    return size;
  }

  // normalize
  static_attest(24 == size);
  if (u8) '=' == out[size - 1] {
    if (u8) '=' == out[size - 2] {
      out[size - 2] = (u8) '/';
      out[size - 1] = 0;
      size--;
    }
  }

  return size;
}

export fn result_encoding_length() -> usize {
  return RESULT_ID_BYTES;
}

fn log10(u32 n) -> usize {
  if n >= 1000000000 { return 9; }
  if n >= 100000000 { return 8; }
  if n >= 10000000 { return 7; }
  if n >= 1000000 { return 6; }
  if n >= 100000 { return 5; }
  if n >= 10000 { return 4; }
  if n >= 1000 { return 3; }
  if n >= 100 { return 2; }
  if n >= 10 { return 1; }
  return 0;
}

fn div10(u32 n) -> u32 {
  u64 inverse = 0x1999999a;
  return (u32) ((inverse * (u64) n) >> 32);
}

fn bcd16(u16 n, u8 mut *out)
  where len(out) >= 3
{
  u8 mut d4 = 0;
  u8 mut d3 = 0;
  u8 mut d2 = 0;
  u8 mut d1 = 0;
  u8 mut d0 = 0;
  u8 mut q = 0;
  //d4 MSD, d0 LSD

  d1 = (u8 )(n>>4)  & 0xf;
  d2 = (u8 )(n>>8)  & 0xf;
  d3 = (u8 )(n>>12) & 0xf;

  d0 = (u8) 6*(d3 + d2 + d1) + (u8) (n & 0xf);
  q = (u8) (d0 * 0xcd) >> 11;
  d0 = (u8) d0 - 10*q;

  d1 = (u8) q + 9*d3 + 5*d2 + d1;
  q = (u8) (d1 * 0xcd) >> 11;
  d1 = (u8) d1 - 10*q;

  d2 = (u8) q + 2*d2;
  q = (u8) (d2 * 0x1a) >> 8;
  d2 = (u8) d2 - 10*q;

  d3 = (u8) q + 4*d3;
  d4 = (u8) (d3 * 0x1a) >> 8;
  d3 = (u8) d3 - 10*d4;

  out[0] = 0x30 + (d4<<4) | (d3);
  out[1] = 0x30 + (d2<<4) | (d1);
  out[2] = 0x30 + (d0<<4);
}
